" https://github.com/rafi/vim-config
" Maintainer: Rafael Bodill

" Runtime and Plugins
" ===

if &compatible
	" vint: -ProhibitSetNoCompatible
	set nocompatible
	" vint: +ProhibitSetNoCompatible
endif

" Set main configuration directory as parent directory
" muzhi: ~/.config/nvim
let $VIM_PATH = fnamemodify(resolve(expand('<sfile>:p')), ':h:h')

function! s:source_file(path, ...)
	" Source user configuration files with set/global sensitivity
	" muzhi: 1
	let use_global = get(a:000, 0, ! has('vim_starting'))
	" echo "-->" use_global
	let abspath = resolve($VIM_PATH . '/' . a:path)
	" muzhi
	" echo "-->" abspath
	if ! use_global
		" echo fnameescape(abspath)
		execute 'source' fnameescape(abspath)
		return
	endif

	let tempfile = tempname()
	" echo "-->" tempfile
	let content = map(readfile(abspath),
		\ "substitute(v:val, '^\\W*\\zsset\\ze\\W', 'setglobal', '')")
	" echo "-->" content
	try
		call writefile(content, tempfile)
		execute printf('source %s', fnameescape(tempfile))
	finally
		if filereadable(tempfile)
			call delete(tempfile)
		endif
	endtry
endfunction

" Initialize startup settings
if has('vim_starting')
	" Use spacebar as leader and ; as secondary-leader
	" Required before loading plugins!
	" let g:mapleader="\<Space>"
	let g:mapleader=","
	let g:maplocalleader=';'

	" Release keymappings prefixes, evict entirely for use of plug-ins.
	nnoremap <Space>  <Nop>
	xnoremap <Space>  <Nop>
	nnoremap ,        <Nop>
	xnoremap ,        <Nop>
	nnoremap ;        <Nop>
	xnoremap ;        <Nop>

	" Vim only, Linux terminal settings
	if ! has('nvim') && ! has('gui_running') && ! has('win32') && ! has('win64')
		call s:source_file('config/terminal.vim')
	endif
endif

" Initialize plugin-manager and load main config files
call s:source_file('config/init.vim')

" Initialize all my configurations
call s:source_file('config/general.vim')
"call s:source_file('config/filetype.vim')
call s:source_file('config/mappings.vim')
"call s:source_file('config/theme.vim')
"colorscheme hybrid_reverse  " change to your favorite theme
colorscheme onedark

" Load user custom local settings
if filereadable($VIM_PATH . '/config/local.vim')
	call s:source_file('config/local.vim')
endif

" vim: set ts=2 sw=2 tw=80 noet :

